plugins {
    id("net.fabricmc.fabric-loom")
}

dependencies {
    minecraft("com.mojang:minecraft:${providers.gradleProperty("minecraft_version").get()}")
    compileOnly("net.fabricmc:fabric-loader:${providers.gradleProperty("loader_version").get()}")
    compileOnly("net.fabricmc.fabric-api:fabric-api:${providers.gradleProperty("fabric_api_version").get()}")
    implementation(project(":core"))
    testImplementation("org.junit.jupiter:junit-jupiter:5.11.4")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation("org.assertj:assertj-core:3.27.0")
}

sourceSets {
    main {
        java {
            srcDir("common/src/main/java")
        }
        resources {
            srcDir("fabric/src/main/resources")
        }
    }
    test {
        java {
            srcDir("common/src/test/java")
        }
    }
}

tasks.withType<JavaCompile>().configureEach {
    options.release = 25
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

tasks.test {
    useJUnitPlatform()
    jvmArgs(
        "--add-modules=jdk.incubator.vector",
        "--add-exports=jdk.incubator.vector/jdk.incubator.vector=ALL-UNNAMED"
    )
}

tasks.processResources {
    val version = providers.gradleProperty("mod_version").get()
    filesMatching("fabric.mod.json") {
        expand("version" to version)
    }
}
